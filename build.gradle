plugins {
    id 'java'
    id 'groovy'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.javamodularity.moduleplugin' version '1.8.15'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.google.osdetector' version '1.7.3'
    id 'org.beryx.runtime' version '1.13.1'
}

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'biz.nellemann.jsshd'
    mainClass = 'biz.nellemann.jsshd.Main'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    modularity.inferModulePath.set(false)
}

javafx {
    version = '21+'
    modules = [ 'javafx.base', 'javafx.controls', 'javafx.fxml' ]
}

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.13'
    implementation 'org.slf4j:slf4j-simple:2.0.13'
    implementation 'org.apache.sshd:sshd-core:2.14.0'
    implementation 'org.apache.sshd:sshd-sftp:2.14.0'
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.1'
    implementation 'org.kordamp.ikonli:ikonli-fontawesome5-pack:12.3.1'
    implementation 'io.github.mkpaz:atlantafx-base:2.0.1'
}

test {
    useJUnitPlatform()
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = [
            'java.security.jgss',
            'java.desktop',
            'java.scripting',
            'java.logging',
            'java.management',
            'jdk.jfr',
            'jdk.unsupported',
            'jdk.crypto.cryptoki',
            'javafx.base',
            'javafx.fxml',
            'javafx.graphics',
            'javafx.controls'
    ]


    launcher {
        noConsole = true
    }

    // Only works with Java 14 (and later)
    jpackage {
        imageName = "jSSHD"
        skipInstaller = true
        installerName = "jSSHD-${osdetector.arch}"
        installerOptions += [
            '--vendor', 'Nellemann Data',
            '--description', 'Minimal SSH File Server',
            '--copyright', 'Mark Nellemann <mark.nellemann@gmail.com>',
            '--app-version', project.findProperty('version'),
        ]

        // Requires: https://wixtoolset.org/ to create installer on Windows
        if(osdetector.os == 'windows') {
            installerType = 'msi'
            skipInstaller = false
            installerOptions += [
                '--win-per-user-install',
                '--win-dir-chooser',
                '--win-menu',
               //'--icon', 'src/main/resources/icon.png'
            ]
        }

        // Requires: xcode-select --install
        if(osdetector.os == 'osx') {
            installerType = 'dmg'
            skipInstaller = false
            installerOptions += [
                //'--icon', 'src/main/resources/icon.icns'
            ]
        }

        // Requires: build-rpm / rpm and dpkg-dev
        if(osdetector.os == 'linux') {
            skipInstaller = false
            installerOptions += [
                    '--linux-shortcut',
                    '--linux-menu-group', 'Internet',
                    '--linux-rpm-license-type', 'APACHE-20',
                    '--linux-deb-maintainer', 'mark.nellemann@gmail.com',
                    //'--icon', 'src/main/resources/icon.png',
            ]
        }

    }

}

/*
tasks.runtime.doLast {
    copy {
        from('src/main/resources')
        into("$buildDir/image/bin")
    }
}*/